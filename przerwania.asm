	org $b700	;$AF00
	
ad_Jadra	:7 dta b(#*16)	
ksztaltJadroA0	dta b(%00000000)
				dta b(%00000000)
				dta b(%01111101)
				dta b(%01111101)
				dta b(%01111101)
				dta b(%01111101)
				dta b(%01111101)
				dta b(%01111101)
				
				dta b(%01111101)
				dta b(%01111101)
				dta b(%01111101)
				dta b(%01111101)
				dta b(%01111101)
				dta b(%01111101)
				dta b(%00000000)
				dta b(%00000000)
		
ksztaltJadroA1	dta b(%00000000)
				dta b(%00000000)
				dta b(%00111101)
				dta b(%00111101)
				dta b(%00111101)
				dta b(%00111101)
				dta b(%00111101)
				dta b(%00111101)
				
				dta b(%00111101)
				dta b(%00111101)
				dta b(%00111101)
				dta b(%00111101)
				dta b(%00111101)
				dta b(%00111101)
				dta b(%00000000)
				dta b(%00000000)
				
ksztaltJadroA2	dta b(%00000000)
				dta b(%00000000)
				dta b(%00001101)
				dta b(%00001101)
				dta b(%00001101)
				dta b(%00001101)
				dta b(%00001101)
				dta b(%00001101)
				
				dta b(%00001101)
				dta b(%00001101)
				dta b(%00001101)
				dta b(%00001101)
				dta b(%00001101)
				dta b(%00001101)
				dta b(%00000000)
				dta b(%00000000)
				
ksztaltJadroA3	dta b(%00000000)
				dta b(%00000000)
				dta b(%00000001)
				dta b(%00000001)
				dta b(%00000001)
				dta b(%00000001)
				dta b(%00000001)
				dta b(%00000001)

				dta b(%00000001)
				dta b(%00000001)
				dta b(%00000001)
				dta b(%00000001)
				dta b(%00000001)
				dta b(%00000001)
				dta b(%00000000)
				dta b(%00000000)
				
ksztaltJadroA4	dta b(%00000000)		
				dta b(%00110000)
				dta b(%00110000)
				dta b(%00111100)
				dta b(%00111100)
				dta b(%11111111)
				dta b(%11111011)
				dta b(%11101011)
	
				dta b(%11101011)		
				dta b(%11101111)
				dta b(%11111111)
				dta b(%00111100)
				dta b(%00111100)
				dta b(%00001100)
				dta b(%00001100)
				dta b(%00000000)	

ksztaltJadroA5	dta b(%00000000)
				dta b(%00000000)
				dta b(%00101101)
				dta b(%00101101)
				dta b(%00101101)
				dta b(%10101101)
				dta b(%10101101)
				dta b(%10101101)
				
				dta b(%10101101)
				dta b(%10101101)
				dta b(%10101101)
				dta b(%00101101)
				dta b(%00101101)
				dta b(%00101101)
				dta b(%00000000)
				dta b(%00000000)

ksztaltJadroA6	dta b(%00000000)
				dta b(%00000000)
				dta b(%00111101)
				dta b(%00111101)
				dta b(%00111101)
				dta b(%10111101)
				dta b(%10111101)
				dta b(%10111101)
				
				dta b(%10111101)
				dta b(%10111101)
				dta b(%10111101)
				dta b(%00111101)
				dta b(%00111101)
				dta b(%00111101)
				dta b(%00000000)
				dta b(%00000000)
				
ksztaltJadroB0	dta b(%00000000)
				dta b(%00111111)
				dta b(%00111111)
				dta b(%00111111)
				dta b(%00111111)
				dta b(%00111111)
				dta b(%00111111)
				dta b(%00000000)
				
				dta b(%00000000)
				dta b(%11011100)
				dta b(%11011100)
				dta b(%11011100)
				dta b(%11011100)
				dta b(%11011100)
				dta b(%11011100)
				dta b(%00000000)
			
ksztaltJadroB1	dta b(%00000000)
				dta b(%00111111)
				dta b(%00111111)
				dta b(%00111111)
				dta b(%00111111)
				dta b(%00000000)
				dta b(%00000000)
				dta b(%00000000)
				
				dta b(%00000000)
				dta b(%11011100)
				dta b(%11011100)
				dta b(%11011100)
				dta b(%11011100)
				dta b(%00000000)
				dta b(%00000000)
				dta b(%00000000)
				
ksztaltJadroB2	dta b(%00000000)
				dta b(%00111111)
				dta b(%00111111)
				dta b(%00000000)
				dta b(%00000000)
				dta b(%00000000)
				dta b(%00000000)
				dta b(%00000000)
				
				dta b(%00000000)
				dta b(%11011100)
				dta b(%11011100)
				dta b(%00000000)
				dta b(%00000000)
				dta b(%00000000)
				dta b(%00000000)
				dta b(%00000000)
				
ksztaltJadroB3	dta b(%00000000)
				dta b(%00111111)
				dta b(%00000000)
				dta b(%00000000)
				dta b(%00000000)
				dta b(%00000000)
				dta b(%00000000)
				dta b(%00000000)
				
				dta b(%00000000)
				dta b(%11011100)
				dta b(%00000000)
				dta b(%00000000)
				dta b(%00000000)
				dta b(%00000000)
				dta b(%00000000)
				dta b(%00000000)
				
ksztaltJadroB4	dta b(%00000000)		
				dta b(%00000011)
				dta b(%00001111)
				dta b(%11111010)
				dta b(%00111110)
				dta b(%00001111)
				dta b(%00000011)
				dta b(%00000000)
	
				dta b(%00000000)			
				dta b(%11000000)
				dta b(%11110000)
				dta b(%10111100)
				dta b(%10101111)
				dta b(%11110000)
				dta b(%11000000)
				dta b(%00000000)
				
ksztaltJadroB5	dta b(%00000000)
				dta b(%00111111)
				dta b(%00111111)
				dta b(%00111111)
				dta b(%00001010)
				dta b(%00001010)
				dta b(%00000010)
				dta b(%00000000)
				
				dta b(%00000000)
				dta b(%11011100)
				dta b(%11011100)
				dta b(%11011100)
				dta b(%10100000)
				dta b(%10100000)
				dta b(%10000000)
				dta b(%00000000)

ksztaltJadroB6	dta b(%00000000)
				dta b(%00111111)
				dta b(%00111111)
				dta b(%00111111)
				dta b(%00111111)
				dta b(%00001010)
				dta b(%00001010)
				dta b(%00000000)
				
				dta b(%00000000)
				dta b(%11011100)
				dta b(%11011100)
				dta b(%11011100)
				dta b(%11011100)
				dta b(%10100000)
				dta b(%10100000)
				dta b(%00000000)

			org $2000
		
hsdlist	dta b(112,$10)
		dta b(64+4),a(hscr)
		dta b(64+4),a(hscr+48)
		dta b(64+2),a(hscr+2*48)
		dta b(64+4),a(hscr+3*48)
		dta b($30)
		dta b(64+2),a(hscr+4*48)
		dta b(64+4),a(hscr+5*48)
		dta b(64+2),a(hscr+6*48)
		dta b(64+4),a(hscr+7*48)
		dta b(64+4),a(hscr+8*48)
		dta b($20)
		dta b(64+2),a(hscr+9*48)
		dta b(64+4),a(hscr+10*48)
		dta b(64+2),a(hscr+11*48)
		dta b(64+4),a(hscr+12*48)
		dta b(64+2),a(hscr+13*48)
		dta b(64+4),a(hscr+14*48)
		dta b(64+2),a(hscr+15*48)
		dta b(64+4),a(hscr+16*48)
		dta b(64+2),a(hscr+17*48)
		dta b(64+4),a(hscr+18*48)
		dta b(64+2),a(hscr+19*48)
		dta b(64+4),a(hscr+20*48)
		dta b(64+2),a(hscr+21*48)
		dta b(64+4),a(hscr+22*48)
		dta b(64+4),a(hscr+23*48)
		dta b(64+4),a(hscr+24*48)
		dta b(64+4),a(hscr+25*48)
		dta b(64+4),a(hscr+26*48)
		dta b(65),a(hsdlist)
		
hsdlist1	dta b(112,$10)
		dta b(64+4),a(hscr)
		dta b(64+4),a(hscr+48)
		dta b(64+2),a(hscr+48*2)		;CONGRATULATIONS !!
		dta b(64+4),a(hscr+48*3)
		dta b($40)
		dta b(64+2),a(hscr+48*4)		;THE HIGH SCORE
		dta b(64+4),a(hscr+48*5)
		dta b($40)
		dta b(64+2),a(hscr+48*6)		;OF THE DAY
		dta b(64+4),a(hscr+48*7)
		dta b(64+4),a(hscr+48*8)
		
		dta b(64+4),a(hscr+48*9)		;WYNIK
		dta b(64+4),a(hscr+48*10)
		dta b(64+4),a(hscr+48*11)
		dta b(64+4),a(hscr+48*12)
		dta b(64+4),a(hscr+48*13)
		dta b(64+4),a(hscr+48*14)
		dta b(64+4),a(hscr+48*15)
		dta b($30)		
		dta b(64+4),a(hscr+48*16)
		dta b(64+4),a(hscr+48*17)
		dta b($40)
		dta b(64+2),a(hscr+48*18)		;GO FOR THE
		dta b(64+4),a(hscr+48*19)
		dta b($40)
		dta b(64+2),a(hscr+48*20)		;SPACE RECORD NOW
		dta b(64+4),a(hscr+48*21)
		dta b(64+4),a(hscr+48*22)
		dta b(65),a(hsdlist1)
		
dlistMatrix	dta b(112,112,112)
			dta b($42),a(obraz1)
			:23 dta b($2)
			dta b(65),a(dlistMatrix)		

	


czyscObraz1	equ *
		lda #128
		ldx #32
@		:29 sta obraz1+#*48,x
		dex
		bpl @-
		
		rts	

czyscObraz2	equ *
		lda #128
		ldx #32
@		:29 sta obraz2+#*48,x
		dex
		bpl @-
		
		rts		
		
nmi		bit $d40f
		bmi dli
		jmp vblk	
	
dli		equ *
		pha

		mwa #pokey $fffe
			
		lda #pozWyniki
		sta HPOSP    ;hiscore
		lda #pozWyniki+8
		sta HPOSP+1
		lda #pozWyniki+16
		sta HPOSP+2
		lda #pozWyniki+24
		sta HPOSM+2
		lda #pozWyniki+26
		sta HPOSM+1
		lda #pozWyniki+28
		sta HPOSM
		lda #0
		sta SIZEM
		;sta IRQEN
			
		lda kolorCzerwony
		sta COLPM
		sta COLPM+1
		sta COLPM+2
		
		mva #36 AUDF4
		sta WSYNC
		
.IF .DEF A800		
		mva #0 IRQEN
.ENDIF
		mva #4 IRQEN
		sta sirq
	
		pla
		rti 
		
pokey	equ *
		sta rejA
		
		mva #35 AUDF4
		lda #kolorLiczby		;pod Hiscore
		sta COLPM
		sta COLPM+1
		sta COLPM+2
		mva #pozWyniki+2 HPOSP
		mva #pozWyniki+10 HPOSP+1
		mva #pozWyniki+18 HPOSP+2
		
		mwa #pokey1 $fffe
		
		
.IF .DEF A800		
		lda #$00
		sta $d20e
		lda #$04
		sta $d20e
.ELSE		
		inc IRQEN
.ENDIF
		
		lda rejA
		rti
		
pokey1	equ *
		sta rejA
		
		
		mva #pozWyniki HPOSP		;1up
		mva #pozWyniki+8 HPOSP+1
kolor1up	equ *+1
		mva #0 COLPM
		sta COLPM+1
		
		mwa #pokey2 $fffe
		mva #35 AUDF4
		
.IF .DEF A800		
		lda #$00
		sta $d20e
		lda #$04
		sta $d20e
.ELSE		
		inc IRQEN
.ENDIF
		
		lda rejA
		rti

pokey2	equ *
		sta rejA
			
		mva #pozWyniki+2 HPOSP		;score
		mva #pozWyniki+10 HPOSP+1
		mva #kolorLiczby COLPM
		sta COLPM+1
		
		mwa #pokey3 $fffe
		
		lda #100
		sta AUDF4
		
.IF .DEF A800		
		lda #$00
		sta $d20e
		lda #$04
		sta $d20e
.ELSE		
		inc IRQEN
.ENDIF
		
		lda rejA
		rti		
	

pokey3	equ *
		sta rejA
			
duszek4pos	equ *+1
		mva #$ff HPOSP+3
		
formacja_stan equ *+1
		lda #$ff
		beq @+
		cmp #5
		bne pk3a
		
@		mva #pozWyniki HPOSM+2
duszek4kolor	equ *+1
		mva #$ff COLPM+3
	
		mwa #pokey4 $fffe
		
		lda #82-1
		sta AUDF4
		
.IF .DEF A800		
		lda #$00
		sta $d20e
		lda #$04
		sta $d20e
.ELSE		
		inc IRQEN
.ENDIF
		
		lda rejA
		rti		

pk3a	mva #pozWyniki-1 HPOSM+2	
		lda conditionColor	;w czasie formacji mryga czerwonym napisem
		sta COLPM
		sta COLPM+1
		sta COLPM+2
		
		mwa #pokey4 $fffe
		
		lda #81
		sta AUDF4
		
.IF .DEF A800		
		lda #$00
		sta $d20e
		lda #$04
		sta $d20e
.ELSE		
		inc IRQEN
.ENDIF
		
		lda rejA
		rti		
		
pokey4	equ *
		sta rejA
		lda #255			;condition napis
		sta AUDF4
conditionColor	equ *+1		
		lda #$ff		;conditionColor
		sta COLPM+1
		sta COLPM+2
conditionColor1	equ *+1
		lda #$ff
		sta COLPM
		
		mwa #pokey5 $fffe
		
.IF .DEF A800		
		lda #$00
		sta $d20e
		lda #$04
		sta $d20e
.ELSE		
		inc IRQEN
.ENDIF
			
		lda rejA
		rti
		
pokey5	equ *
		sta rejA
		
		
		mva kolorMapa	COLPM+3
		mva #pozWyniki-1 HPOSP+3
			
		mva #kolorLiczby COLPM
		
		lda #16+4
		sta SIZEM
		mva kolorCzerwony COLPM+1
		
		sta COLPM+2
		mva #pozWyniki+19 HPOSM+2
		mva #pozWyniki+23 HPOSM+1
		
radarX1	equ *+1
		mva #$ff HPOSM  ;pozycja statku na radarze
		mva #1 SIZEP+2		;przeciwnicy na radarze
		mva #pozWyniki+3 HPOSP+2
		
		
		lda #185
		sta AUDF4
		
		mwa #pokey5a $fffe
		
.IF .DEF A800		
		lda #$00
		sta $d20e
		lda #$04
		sta $d20e
.ELSE		
		inc IRQEN
.ENDIF
		
		lda startMapy
		cmp #10
		bcs @+		
		
		lda #108 	;pozWyniki-1-68		;statek srodek 108
		sta HPOSP
		sta HPOSP+1	
		
		lda rejA
		rti
		
@		mva #0 HPOSP
		sta HPOSP+1
		
		lda rejA
		rti
		
pokey5a	equ *
		sta rejA
		
		lda #110-18
		sta AUDF4
		mwa #pokey6 $fffe
		
.IF .DEF A800		
		lda #$00
		sta $d20e
		lda #$04
		sta $d20e
.ELSE		
		inc IRQEN
.ENDIF
		
		lda rejA
		rti
		
pokey6	equ *
		sta rejA

		
		mva #$0d COLPM	
		sta COLPM+1
		
		sta COLPM+2
		mva #0 COLPM+3		;koniec mapy zapasowe statki
		
		sta SIZEP+2
.IF .DEF A800
		sta $d20e
.ENDIF
		mva #1+4+16+64 SIZEM
				;zakonczenie mapy
		
poz2lives equ *+1
		mva #$ff HPOSP+2		
		mva #$ff HPOSP+1
		mva #$ff HPOSP
poz2mlives	equ *+1		
		mva #$ff HPOSM+2
		mva #$ff HPOSM+1
		mva #$ff HPOSM
		mva #1+16 GTIACTL
		
		mwa #pokey7 $fffe
		
		lda #26
		sta AUDF4
		
.IF .DEF A800		
		lda #$04
		sta $d20e
.ELSE		
		inc IRQEN
.ENDIF
		
		lda rejA
		rti
			

	
pokey7	equ *
		sta rejA
		
		;mwa #pokey $fffe
		
		
		lda #$00
		sta AUDF4
		sta SIZEM
		sta $d20e
		sta sirq
		
		mva #1 GTIACTL
		mva #pozWyniki HPOSP
		mva #pozWyniki+8 HPOSP+1
		mva #pozWyniki+16 HPOSM+1		;Round
		mva #pozWyniki+18 HPOSM
		mva #pozWyniki+22 HPOSP+2
		mva kolorLevel COLPM+2
		mva kolorRound COLPM
		sta COLPM+1
		
		lda rejA
		rti
	

formacja_typ	dta b(0)
formacja_radar	dta b(0)
formacja_maska	dta b(0)
formacja_speed	dta b(0)
czy_6enemy		dta b(0)
opoz_denemy		dta b(0) ;jak często pojawia się nowy enemy
opoz_dlosuj		dta b(0)	 ;jak czesto pojawiaja sie spy i formacja
circle			dta b(0)		;czy zaliczono wszystkie levele
kat1			dta b(0)
kat2			dta b(0)
	
mryganieRadarX	dta b(0)

sfx_effect	dta b(255)
sfx_dzialko dta b(255)
sfx_asteroid	dta b(255)
sfx_bomba		dta b(255)
sfx_wybuch	dta b(255)
sfx_dead	dta b(255)
sfx_antyair	dta b(255)
sfx_enemyHit	dta b(255)
sfx_rakieta		dta b(0)
sfx_extra		dta b(0)
extraLicznik	dta b(0)
sfxlicznik1	dta b(0)
licznikStrzal	dta b(0)
RadarX			dta b(0)		;pozycja statku na mapie
bufore3			equ sfx_effect


speech	dta b(0)
;rmt_goto	equ MODUL+$65F
licznikMrygFormacja	dta b(25)

sounds		equ *	
@			lda extraLicznik
			beq @+
			dec extraLicznik
			jmp @+3				;pomin dzwiek silnika i strzaly
			
@			lda sfx_extra
			bmi @+
			mva #30 extraLicznik		;czas trwania
			ldx #0*2
			ldy #13*2
			jsr add_fx
			mva #255 sfx_extra
			sta sfx_effect
			jmp @+2

@			lda licznikStrzal
			beq @+
			dec licznikStrzal
			bne @+
			ldy #16*2			;dzwiek silnika statku
			ldx #0*2	
			jsr add_fx

@			ldy sfx_effect
			bmi @+
											;dzwiek strzalu statku

			ldx #0*2
			jsr add_fx

			lda #$ff
			sta sfx_effect				

@			lda sfxlicznik1
			beq @+
			dec sfxlicznik1
			
@			lda formacja_stan
			cmp #3
			beq @+
			cmp #4			;formacja gdy goni ma wartosc 3 lub 4
			bne @+1
@			lda sfxlicznik1
			cmp #40
			bne @+
			ldy #14*2			;dzwiek alarmu, wlacz po wybuchu
			ldx #1*2		
			jsr add_fx
			
@			ldy sfx_wybuch
			bmi @+
			
			mva #70 sfxLicznik1
			ldy sfx_wybuch
			ldx #1*2
			jsr add_fx
			
			lda #$ff
			sta sfx_wybuch

@			lda formacja_stan			;czy aby nie ma dzwieku formacji?
			cmp #3
			beq @+1
			cmp #4
			beq @+1
			
			ldy sfx_rakieta			
			bmi @+
			lda sfxlicznik1			;jesli licznik <40 to dodaj dzwiek rakiety
			cmp #40
			bcs @+
			
			mva #39 sfxlicznik1
			ldx #1*2
			jsr add_fx
			jmp @+1
			
			
@			ldy sfx_antyair
			bmi @+1 
			
			lda sfxlicznik1
			bne @+
			
			ldx #1*2						;jesli nie ma zadnych innych dzwiekow to dodaj dzwiek strzalu dzialka
			jsr add_fx
			
@			lda #$ff
			sta sfx_antyair
			sta sfx_rakieta
			
			
			
@			ldy sfx_dzialko		;wybuch dzialka bazy
			bmi @+
			
			ldx #2*2
			jsr add_fx
			jmp eChannel2
			
@			ldy sfx_bomba
			bmi @+
			
			ldx #2*2
			jsr add_fx
			jmp eChannel2			
			
@			ldy sfx_enemyHit
			bmi @+
			
			ldx #2*2
			jsr add_fx
			jmp eChannel2
			
@			ldy sfx_asteroid
			bmi @+
			
			ldx #2*2
			jsr add_fx		


eChannel2	equ *
			lda #$ff
			sta sfx_asteroid
			sta sfx_enemyHit
			sta sfx_bomba
			sta sfx_dzialko

			


@			ldy sfx_dead
			bmi @+
			
			
			ldx #0*2
			jsr add_fx
			
			lda #$ff
			sta sfx_dead
			
			
@			lda muzyka
			beq graj
			cmp #1
			bne @+
			
			ldx <(rmt_goto+$0*4)		
			ldy >(rmt_goto+$0*4)
			stx p_song
			sty p_song+1
			mva #BANK4 PORTB
			jsr GetSongLine
			mva #BANK0 PORTB
			mva #0 sfx
			jmp graj
			

@			lda muzyka
			cmp #2
			bne @+
			
			ldx <(rmt_goto+$4*4)		;$1e=30,
			ldy >(rmt_goto+$4*4)
			stx p_song
			sty p_song+1
			mva #BANK4 PORTB
			jsr GetSongLine
			mva #BANK0 PORTB
			mva #0 sfx
@			equ *
						
graj		mva #0 muzyka
			rts	
			
;przerwanie vblk
vblk		equ *
			sta vblA

			inc zegar
			
			lda stan_gry
			beq vblk1
			cmp #1
			bne @+
			
			jsr startPanel
			
			lda ntscf
			beq _ntsc1
			dec kolpom0
			bne _ntsc1
			mva #6 kolpom0
			jmp vblk1
_ntsc1			equ *

			
			stx vblX
			sty vblY
			lda PORTB
			pha
			mva #BANK4 PORTB
	
			
			jsr rmt_play
			
			pla
			sta PORTB
			ldx vblX
			ldy vblY
			
			jmp vblk1

@			equ *
			lda pauza
			bne @+			;podczas pauzy nie odgrywaj dzwiekow

_vbsnd1		equ *+1			
			lda #$00		;zamiast SETPOKEY
			sta $d200
_vbsnd2		equ *+1			
			lda #$00
			sta $d201
_vbsnd3		equ *+1			
			lda #$00
			sta $d200+2
_vbsnd4		equ *+1			
			lda #$00
			sta $d201+2
_vbsnd5		equ *+1			
			lda #$00
			sta $d200+4
_vbsnd6		equ *+1			
			lda #$00
			sta $d201+4
			
_vbsnd7		equ *+1			
			lda #$00
			;sta $d208		
			
@			lda vblFlaga
			beq vblk1
	
			lda trafienie		
			ora startMapy
			bne vblk1
			
			pla
			sta znacznik
			pla
			sta vb_ad+1		;adres powrotu
			pla
			sta vb_ad+2
			beq vblk2
			
				
			lda #>vb_strzal
			pha
			lda #<vb_strzal		;adres pod ktory skoczymy po zakonczeniu przerwania
			pha	
			lda znacznik
			pha					;znacznik

			rti					;opusc przerwanie
			
vblk1		equ *
			
			lda vblA

			rti
			
vblk2			
			pha					;jeślśli vblk przerwie irq to pomin strzal
			lda vb_ad+1
			pha
			lda znacznik
			pha
			lda vblA
			dec vblFlaga
			rti			

startPanel	equ *
			lda #pozWyniki
			sta HPOSP    ;hiscore
			lda #pozWyniki+8
			sta HPOSP+1
			lda #pozWyniki+16
			sta HPOSP+2
			lda #pozWyniki+24
			sta HPOSM+2
			lda #pozWyniki+26
			sta HPOSM+1
			lda #pozWyniki+28
			sta HPOSM
			lda #0
			sta SIZEM
			sta SIZEP
			sta SIZEP+1
			sta SIZEP+2
			
			lda kolorCzerwony
			sta COLPM
			sta COLPM+1
			sta COLPM+2
			rts
			
;wywołanie procedury podczas przerwania vblk			
vb_strzal	equ *	
			php
			cld		
			
			stx vblX
			sty vblY
			dec ramka
			lda ramka4
			eor #4
			sta ramka4
			
			jsr printStrzal		
			
			inc ramka
			lda ramka4
			eor #4
			sta ramka4
			
			dec vblFlaga
			
			ldx vblX		;przywroc rejestry i znaczniki
			ldy vblY
			lda vblA
			plp
vb_ad		jmp $ffff


panelTab	dta b(0,$e2,$32,$b2,$00,$82)		
		
		
playerNapis2	dta b(116,121,117,123,0,0,125,118,123,124)

zmaz_radar	equ *
		ldy rads		;wymaz pozycje statku/formacji na radarze
		lda sprites+$300+startWyniki+84,y
		and #%11111100
		sta sprites+$300+startWyniki+84,y
		lda sprites+$300+startWyniki+84+1,y
		and #%11111100
		sta sprites+$300+startWyniki+84+1,y
		lda sprites+$300+startWyniki+84+2,y		;dodatkowe 2 punkty zmazujemy po formacji
		and #%11111100
		sta sprites+$300+startWyniki+84+2,y
		rts
		
game_over	equ *
		mva #0 stan_gry		;nie graj dzwiekow
		sta AUDC1
		sta AUDC2
		sta AUDC3
		
		jsr sumaPunkty		;dodaj i wyświetl punkty jeśli gra nie zdążyła dodać
		lda scoreZmiana
		bne @+
		jsr piszScore1		
		jsr czyHscore
@		equ *
		
		jsr livesRedOff
		
		mva #0 sfxlicznik1

@		ldx #104		;12znakow
@		lda player1chars-1,x
		sta znaki1-1+115*8,x
		sta znaki2-1+115*8,x
		dex
		bne @-
		
		ldx #9
@		lda playerNapis2,x
		beq @+
		ora #128
		sta obraz1+11+14*48,x
		sta obraz2+11+14*48,x
@		dex
		bpl @-1
;sampel	
		jsr waitvbl
		;mva #58 DMACTL
		
		ldy #20
		jsr wait
		
	
		mwa sample+2 sam		;poczatek sampla	
		mva sample2adr probka+1
		
		mva #%01000000 NMIEN
		mva #1 powtorz		;wlaczony sampel
		mva #4 rodzajSpeech
		
		jsr schowaj_duszki
		
		mwa #pok0 $fffe		;nowy wektor przerwania
		
		mva #15 AUDF4		;jak czesto wywolujemy przerwanie, 64khz/16 = 4khz
		
		mva #0 IRQEN
		mva #4 sirq
		sta IRQEN
		
		ldy zegar
@		cpy zegar
		beq @+
		ldy zegar
		
		lda kolorPanel
		adc #1
		and #%1111
		sta kolorPanel
		ldx rodzajSpeech
		ora panelTab,x
		sta COLPM+2 
		
@		lda sirq
		bne @-1
		
		jsr waitvbl
		mva #0 DMACTL
		
		ldx #15
		lda #0
@		sta sprites1+$660,x
		dex
		bpl @-
		
			
@		sta formacja_stan		;A=0
		jsr pokaz_duszki
			
@		pla		;usuwamy adres powrotu po jsr PrintShip
		pla
		
		mva #BANK4 PORTB
		jmp gameover1


hscNick1	dta c'A',b(91),b(94,0)
hscNick2	dta c'T',b(91),b(96,0)
hscNick3	dta c'A',b(91),b(93,0)
hscNick4	dta c'R',b(91),c'X',b(0)
hscNick5	dta c'I',b(91),c'E',b(0)	
hscScore1	dta b($00,$20,$00),(2)	
hscScore2	dta b($00,$20,$00),(2)
hscScore3	dta b($00,$20,$00),(2)
hscScore4	dta b($00,$20,$00),(2)
hscScore5	dta b($00,$20,$00),(2)


;inicjuje efekty dzwiekowe
init_fx	equ *
		mva #BANK4 PORTB
		ldx #<MODUL					;low byte of RMT module to X reg
		ldy #>MODUL					;hi byte of RMT module to Y reg
		lda #2						;starting song line 0-255 to A reg 	
		jsr RASTERMUSICTRACKER
		mva #BANK0 PORTB
		rts
		
playFX	equ *		;dodaje fx'y do kolejki
		mva #BANK4 PORTB

		
		jsr RMT_P2
		
		lda trackn_audf+0		;zamiast SETPOKEY
		sta _vbsnd1
		lda trackn_audc+0
		sta _vbsnd2
		lda trackn_audf+1	
		sta _vbsnd3
		lda trackn_audc+1
		sta _vbsnd4
		lda trackn_audf+2
		sta _vbsnd5
		lda trackn_audc+2
		sta _vbsnd6
			
		;lda v_audctl
		;sta _vbsnd7	 
			
		mva #BANK0 PORTB
		rts
		
cisza	equ *
		bne @+
		ldx #1*2			;graj formacje
		ldy #2*14
		jsr add_fx
		rts
		
@		lda #0				;wycisz formacje
		sta kanal2
		sta AUDC2
		sta _vbsnd4
		rts
		
		mvx #BANK4 PORTB
		ldy #14*2		;wycisz alarm formacji A=128 i wlacz A=0
		ldx #1
		jsr RASTERMUSICTRACKER+15
		mvx #BANK0 PORTB
		rts
		
mute	equ *	

		mvx #BANK4 PORTB
		ldx <(rmt_goto+$02*4)		
		ldy >(rmt_goto+$02*4)
		stx p_song
		sty p_song+1
		jsr GetSongLine				
		mvx #BANK0 PORTB
		rts
		
wait	equ *	;czas w Y
		tya
		clc
		adc zegar
@		cmp zegar
		bne @-
		rts
		
waitvbl	equ *
		lda zegar
@		cmp zegar
		beq @-
		rts
		
matrixOn	equ *
		jsr waitvbl
		mva #0 DMACTL
		mva #BANK3 PORTB
		jsr printDataMatrix
		jsr waitvbl
		mva #0 DMACTL
		mva #3 PMCTL
		
		mva #BANK4 PORTB
		jmp Title
		
		
		